import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext.kotlin_version = '2.2.20'

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:compose-compiler-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:+"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

// Configure Java toolchain for all projects
subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')) {
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }

        if (project.plugins.hasPlugin('org.jetbrains.kotlin.android')) {
            project.tasks.withType(KotlinCompile).tap {
                configureEach {
                    kotlinOptions {
                        jvmTarget = '17'
                    }
                }
            }
        }
    }
}